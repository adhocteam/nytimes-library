# GCP API is VERY limited in what you can create for OAuth. Most of the steps needed to deploy the OAuth configuration must be done on console.
resource "google_iap_brand" "main" {
  support_email     = var.support_email
  application_title = var.application_title
}

# A Oauth client must be created manually on the console or via gcloud commands?

# Optionally create a secrets manager resource to store the credentials generated by the user

resource "google_secret_manager_secret" "main_client_id" {
  count     = var.use_secretsmanager ? 1 : 0
  secret_id = "${var.secretsmanager_resource_prefix}-oauth-client-id"
  replication {
    automatic = true
  }
}

resource "google_secret_manager_secret" "main_client_secret" {
  count     = var.use_secretsmanager ? 1 : 0
  secret_id = "${var.secretsmanager_resource_prefix}-oauth-client-secret"
  replication {
    automatic = true
  }
}

# Create a random string used for the Library app's oauth session secret string

resource "random_password" "session_secret" {
  count            = var.use_secretsmanager ? 1 : 0
  length           = 48
  special          = true
  min_special      = 6
  override_special = "#_%^"
  keepers = {
    pass_version = 1
  }
}

resource "google_secret_manager_secret" "main_session_secret" {
  count     = var.use_secretsmanager ? 1 : 0
  secret_id = "${var.secretsmanager_resource_prefix}-oauth-session-secret"
  replication {
    automatic = true
  }
}

resource "google_secret_manager_secret_version" "main_session_secret" {
  count  = var.use_secretsmanager ? 1 : 0
  secret = google_secret_manager_secret.main_session_secret[0].id

  secret_data = random_password.session_secret[0].result
}
